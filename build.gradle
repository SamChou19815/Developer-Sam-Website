/*
 * --------------------------------------------------------------------------------
 * Part 0: General Info Declaration
 * --------------------------------------------------------------------------------
 */

plugins {
    // 3rd-parties
    id 'org.jetbrains.kotlin.jvm' version '1.2.60'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'com.google.cloud.tools.jib' version '0.9.2'
}

group = 'com.developersam'
version = '5.0.0'

/*
 * --------------------------------------------------------------------------------
 * Part 1: Common Declaration for all Projects
 * --------------------------------------------------------------------------------
 */

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    configurations {
        // Suppress Old Guava
        all*.exclude group: 'com.google.guava', module:'guava-jdk5'
    }

    dependencies {
        // Kotlin
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        // Test
        testImplementation 'junit:junit:4.12'
        // Kinetics
        api 'com.github.SamChou19815:kinetics:0.4.0'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjvm-default=enable"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjvm-default=enable"]
        }
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    shadowJar {
        classifier = null
    }
}

/*
 * --------------------------------------------------------------------------------
 * Part 2: Dependencies for Main
 * --------------------------------------------------------------------------------
 */

dependencies {
    // Spark
    implementation 'com.sparkjava:spark-core:2.7.2'
    implementation 'com.sparkjava:spark-kotlin:1.0.0-alpha'
    // Public Apps Backend
    implementation 'com.github.SamChou19815:sampl:0.0.5'
    implementation 'com.github.SamChou19815:chunk-reader:0.2.2'
    implementation 'com.developersam:game-ten:1.0.1'
    // Sub-modules
    implementation project(':backend-scheduler')
    implementation project(':backend-rss-reader')
}

/*
 * --------------------------------------------------------------------------------
 * Part 3: Config for Building JVM Project
 * --------------------------------------------------------------------------------
 */

shadowJar {
    baseName = "website"
    version = 5.0
    manifest {
        attributes 'Main-Class': 'com.developersam.main.WebApp'
    }
    zip64 true
    artifacts {
        shadow(tasks.shadowJar.archivePath) {
            builtBy shadowJar
        }
    }
}

test {
    testLogging {
        showStandardStreams = true
        events = ['passed', 'skipped', 'failed']
    }
}

project.tasks.assemble.dependsOn(shadowJar)
project.tasks.jib.dependsOn(build)

jib {
    from {
        image = 'openjdk:8u171-jre-alpine3.7'
        credHelper = 'docker-credential-osxkeychain'
    }
    to {
        image = 'gcr.io/dev-sam/backend-container'
        credHelper = 'docker-credential-gcr'
    }
    container {
        mainClass = 'com.developersam.main.WebApp'
        ports = ['1000', '2000-2003/udp', '8080']
    }
}

/*
 * --------------------------------------------------------------------------------
 * Part 4: Tasks to Build and Deploy Other Parts
 * --------------------------------------------------------------------------------
 */

task updateDatastoreIndices {
    String dir = '~/.config/gcloud/emulators/datastore/WEB-INF'
    String indexFile = "$dir/index.yaml"
    doLast {
        exec {
            commandLine 'sh', '-c', "gcloud datastore create-indexes $indexFile"
        }
    }
}

task localBuildAndRunBackendContainer {
    String stageDir = 'build/staging'
    String imageTag = 'local-test-image'
    String containerName = 'local-test-container'
    String buildCommand = "docker build -t $imageTag ."
    String stopCommand = "docker stop $containerName"
    String removeCommand = "docker rm $containerName"
    String runCommand = "docker run -p 8080:8080 --name $containerName $imageTag"
    String cmd = "cd build/staging; $buildCommand; $stopCommand; $removeCommand; $runCommand"
    doLast {
        exec {
            commandLine 'sh', '-c', "mkdir -p $stageDir"
        }
        exec {
            commandLine 'sh', '-c', "cp build/libs/website-5.0-all.jar $stageDir"
        }
        exec {
            commandLine 'sh', '-c', "cp src/main/docker/* $stageDir"
        }
        exec {
            commandLine 'sh', '-c', cmd
        }
    }
}

localBuildAndRunBackendContainer.dependsOn(build)
