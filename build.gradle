buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
    }
}

plugins {
    // Cores
    id 'java'
    id 'idea'
    // 3rd-parties
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'com.google.cloud.tools.appengine'

group = 'com.developersam.web'
version = '4.0.0'

sourceCompatibility = 1.8  // App Engine Standard uses Java 8
targetCompatibility = 1.8  // App Engine Standard uses Java 8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'junit:junit:4.12'
    // Vert.x
    implementation 'io.vertx:vertx-core:+'
    implementation 'io.vertx:vertx-web:+'
    implementation 'io.vertx:vertx-lang-kotlin:+'
    // Google Cloud API
    implementation 'com.google.cloud:google-cloud-datastore:+'
    implementation 'com.google.cloud:google-cloud-language:+'
    // Add your dependencies here.
    // GSON
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.danilopianini:gson-extras:0.2.1'
    // Logging
    implementation 'org.slf4j:slf4j-api:+'
    implementation 'org.slf4j:jcl-over-slf4j:+'
    implementation 'org.slf4j:slf4j-log4j12:+'
    implementation 'log4j:log4j:+'
    // My Libraries
    implementation 'com.github.SamChou19815:TypedStore:master-SNAPSHOT'
    implementation 'com.github.SamChou19815:vert-fire:0.3.0'
    implementation 'com.github.SamChou19815:vertx-gcp-datastore:0.2.0'
    // Apps Backend
    implementation 'com.developersam:game-ten:1.0.1'
}

shadowJar {
    baseName = "Developer-Sam-Website"
    version = 4.0
    manifest {
        attributes 'Main-Class': 'com.developersam.main.WebServer'
    }
}

project.tasks.assemble.dependsOn project.tasks.shadowJar

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

appengine {

    stage {
        artifact = "build/libs/Developer-Sam-Website-4.0-all.jar"
    }

    deploy {
        stopPreviousVersion = true
        promote = true
        version = 4
    }

}

