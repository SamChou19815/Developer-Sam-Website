buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
    }
}

plugins {
    // Cores
    id 'java'
    id 'idea'
    // 3rd-parties
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'com.google.cloud.tools.appengine'

group = 'com.developersam'
version = '5.0.0'

sourceCompatibility = 1.8  // App Engine Standard uses Java 8
targetCompatibility = 1.8  // App Engine Standard uses Java 8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'junit:junit:4.12'
    // Spark
    implementation "com.sparkjava:spark-core:2.7.2"
    implementation "com.sparkjava:spark-kotlin:1.0.0-alpha"
    // Google Cloud API
    implementation 'com.google.cloud:google-cloud-datastore:+'
    implementation 'com.google.cloud:google-cloud-language:+'
    // Firebase
    implementation 'com.google.firebase:firebase-admin:6.2.0'
    // GSON
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.danilopianini:gson-extras:0.2.1'
    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.25'
    implementation 'org.slf4j:slf4j-log4j12:1.7.25'
    implementation 'log4j:log4j:1.2.17'
    // My Libraries
    implementation 'com.github.SamChou19815:TypedStore:80befecd10'
    // Apps Backend
    implementation 'com.developersam:game-ten:1.0.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.developersam.main.WebApp'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    baseName = "website"
    version = 5.0
    manifest {
        attributes 'Main-Class': 'com.developersam.main.WebApp'
    }
}

project.tasks.assemble.dependsOn project.tasks.shadowJar

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

appengine {

    stage {
        artifact = "build/libs/website-5.0-all.jar"
    }

    deploy {
        stopPreviousVersion = true
        promote = true
        version = 5
    }

}
