buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
    }
}

plugins {
    // 3rd-parties
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group = 'com.developersam'
version = '5.0.0'

dependencies {
    compile project(':backend-main')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    dependencies {
        // Kotlin
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        // Test
        testImplementation 'junit:junit:4.12'
        // DB
        implementation 'com.github.SamChou19815:TypedStore:0.1.0'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjvm-default=enable"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjvm-default=enable"]
        }
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    shadowJar {
        classifier = null
    }
}

shadowJar {
    baseName = "website"
    version = 5.0
    manifest {
        attributes 'Main-Class': 'com.developersam.main.WebApp'
    }
    zip64 true
    artifacts {
        shadow(tasks.shadowJar.archivePath) {
            builtBy shadowJar
        }
    }
}

build.dependsOn(shadowJar)

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
